/*=======================================================================
 * Version: $Id: confFile.l,v 1.2 2014/11/13 16:36:57 nroche Exp $
 * Project: Mediatex
 * Module : conf scanner

 * configuration scanner

 MediaTex is an Electronic Records Management System
 Copyright (C) 2014  Nicolas Roche
 
 This program is free software: you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation, either version 3 of the License, or
 any later version.
 
 This program is distributed in the hope that it will be useful,
 but WITHOUT ANY WARRANTY; without even the implied warranty of
 MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 GNU General Public License for more details.
 
 You should have received a copy of the GNU General Public License
 along with this program.  If not, see <http://www.gnu.org/licenses/>.
 =======================================================================*/

/* definitions: ========================================================*/

/* Options for Specifing Filenames: */
%option outfile="confFile.c"
%option header-file="confFile.h"

/* Options Affecting Scanner Behavior: */
%option case-insensitive
%option nolex-compat
/* option batch OU interactive */
%option batch
%option 8bit
%option noposix-compat
%option nostdinit
%option noyywrap

/* Code-Level And API Options: */
%option ansi-definitions
%option ansi-prototypes
%option reentrant
%option bison-bridge
%option noc++
%option pointer
%option prefix="conf_"
%option nomain
%option unistd

/* Options for Scanner Speed and Size: */
%option noalign
%option ecs
%option nometa-ecs
%option noread

/* Debugging Options: */
%option nobackup
%option debug
%option noperf-report
%option warn

/* Miscellaneous Options: */
%option nounput

%top{
/*=======================================================================
 * Version: this file is generated by LEX using confFile.l
 * Project: Mediatex
 * Module : conf scanner

 * configuration scanner

 MediaTex is an Electronic Records Management System
 Copyright (C) 2014  Nicolas Roche
 
 This program is free software: you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation, either version 3 of the License, or
 any later version.
 
 This program is distributed in the hope that it will be useful,
 but WITHOUT ANY WARRANTY; without even the implied warranty of
 MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 GNU General Public License for more details.
 
 You should have received a copy of the GNU General Public License
 along with this program.  If not, see <http://www.gnu.org/licenses/>.
 =======================================================================*/

#include "../mediatex.h"
#include "../misc/log.h"
#include "../memory/strdsm.h"

#ifndef utMAIN
#include "confFile.tab.h"
#endif	/*	: utMAIN	*/

typedef struct ConfExtra {
  int            lineNo;
  // nothing more needed into the scanner
} ConfExtra;

// %union{} used by flex/bison to write/read values
// (only available when linked with bison)
typedef union ConfBisonUnion {
  unsigned int  number;
  float         score;
  off_t         size;
  time_t        time;
  char*         string;
} ConfBisonUnion;

#define YYSTYPE ConfBisonUnion
#define YY_NO_INPUT // no warning
}

%{
  /* no local variables: reentrant */
%}

 /* scaner states */
%x USERVALUE
%x COLLECTION

%%
 /* rules: ==========================================================*/
   
%{
  /* no local variables: reentrant */
%}

 /* first rule: */
<INITIAL>{

  /* global settings */

  host {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: HOST", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confHOST);
#endif	/*	: utMAIN	*/
  }

 networks {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: NETWORKS", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confNETWORKS);
#endif	/*	: utMAIN	*/
  }

 gateways {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: GATEWAYS", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confGATEWAYS);
#endif	/*	: utMAIN	*/
  }

  mdtxport {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: MDTXPORT", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confMDTXPORT);
#endif	/*	: utMAIN	*/
  }

  sshport {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: SSHPORT", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confSSHPORT);
#endif	/*	: utMAIN	*/
  }

  cachesize {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: CACHE_SIZE", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confCACHESIZE);
#endif	/*	: utMAIN	*/
  }

  cachettl {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: CACHE_TTL", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confCACHETTL);
#endif	/*	: utMAIN	*/
  }

  queryttl {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: QUERY_TTL", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confQUERYTTL);
#endif	/*	: utMAIN	*/
  }

  checkttl {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: CHECK_TTL", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confCHECKTTL);
#endif	/*	: utMAIN	*/
  }

  suppttl {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: SUPP_TTL", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confSUPPTTL);
#endif	/*	: utMAIN	*/
  }

  maxscore {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: MAXSCORE", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confMAXSCORE);
#endif	/*	: utMAIN	*/
  }

  badscore {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: BADSCORE", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confBADSCORE);
#endif	/*	: utMAIN	*/
  }

  powsupp {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: POWSUPP", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confPOWSUPP);
#endif	/*	: utMAIN	*/
  }

  factsupp {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: FACTSUPP", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confFACTSUPP);
#endif	/*	: utMAIN	*/
  }

  comment {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: COMMENT", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confCOMMENT);
#endif	/*	: utMAIN	*/
  }

  /* stanzas */
  coll(ection)? {
    BEGIN(COLLECTION);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: COLL", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confCOLL);
#endif	/*	: utMAIN	*/
  }

  localhost {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: LOCALHOST", 
	    ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confLOCALHOST);
#endif	/*	: utMAIN	*/
  }

  share {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: SHARE", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confSHARE);
#endif	/*	: utMAIN	*/
  }

  , {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: COMMA", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confCOMMA);
#endif	/*	: utMAIN	*/
  }

  - { 
    BEGIN(COLLECTION);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: MINUS", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confMINUS);
#endif	/*	: utMAIN	*/
  }

  @ { 
    BEGIN(COLLECTION);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: AROBASE", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confAROBASE);
#endif	/*	: utMAIN	*/
  }

  : {
    BEGIN(USERVALUE);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: COLON", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confCOLON);
#endif	/*	: utMAIN	*/
  }

  end(block)? {
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: ENDBLOCK", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    return(confENDBLOCK);
#endif	/*	: utMAIN	*/
  }

  g(o)? {
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: GIGA", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    yylval->size = GIGA;
    return(confSIZE);
#endif	/*	: utMAIN	*/
  }

  m(o)? {
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: MEGA", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
     yylval->size = MEGA;
     return(confSIZE);
#endif	/*	: utMAIN	*/
  }

  k(o)? {
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: KILO", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    yylval->size = KILO;
    return(confSIZE);
#endif	/*	: utMAIN	*/
  }

  o {
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: OCTET", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    yylval->size = 1;
    return(confSIZE);
#endif	/*	: utMAIN	*/
  }

  y(ear)? {
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: YEAR", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    yylval->time = YEAR;
    return(confTIME);
#endif	/*	: utMAIN	*/
  }  

  mon(th)? {
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: MONTH", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    yylval->time = MONTH;
    return(confTIME);
#endif	/*	: utMAIN	*/
  }

  w(eek)? {
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: WEEK", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    yylval->time = WEEK;
    return(confTIME);
#endif	/*	: utMAIN	*/
  }

  d(ay)? {
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: DAY", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    yylval->time = DAY;
    return(confTIME);
#endif	/*	: utMAIN	*/
  }

  h(our)? {
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: HOUR", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    yylval->time = HOUR;
    return(confTIME);
#endif	/*	: utMAIN	*/
  }

  min|(nute) {
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: MINUTE", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    yylval->time = MINUTE;
    return(confTIME);
#endif	/*	: utMAIN	*/
  }

  s(econde)? {
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: SECONDE", ((ConfExtra*)yyextra)->lineNo);
#else	/*	: utMAIN	*/
    yylval->time = 1;
    return(confTIME);
#endif	/*	: utMAIN	*/
  }
 }

 /* Allow to use a value wich begin by a token name */
<USERVALUE>{

  [+-]?[[:digit:]^]+\.[+-]?[[:digit:]^]+ {
    BEGIN(INITIAL);
    float score = 0;
    sscanf(yytext, "%f", &score);
#ifdef utMAIN
    logEmit(LOG_INFO, "line %i: SCORE=  %.2f", 
	    ((ConfExtra*)yyextra)->lineNo, score);
#else	/*	: utMAIN	*/
    yylval->score = score;
    return(confSCORE);
#endif	/*	: utMAIN	*/
  }

  [+-]?[[:digit:]^]+ {
    BEGIN(INITIAL);
    unsigned int number = 0;
    number = strtoul(yytext, (char**)0, 10);
#ifdef utMAIN
    logEmit(LOG_INFO, "line %i: NUMBER=  %i", 
	    ((ConfExtra*)yyextra)->lineNo, number);
#else	/*	: utMAIN	*/
    yylval->number = number;
    return(confNUMBER);
#endif	/*	: utMAIN	*/
  }

  \"[^\n\"]*\" { 
    BEGIN(INITIAL);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: ID=     '%s'", 
	    ((ConfExtra*)yyextra)->lineNo, yytext);
#else	/*	: utMAIN	*/
    yylval->string = createSizedString(yyleng-2, yytext+1);
    return(confSTRING);
#endif	/*	: utMAIN	*/
  }

  [^[:blank:],\r\n]+ {
    BEGIN(INITIAL);
#ifdef utMAIN
    logEmit(LOG_DEBUG, "line %i: ID=     '%s'", 
	    ((ConfExtra*)yyextra)->lineNo, yytext);
#else	/*	: utMAIN	*/
    yylval->string = createSizedString(yyleng, yytext);
    return(confSTRING);
#endif	/*	: utMAIN	*/
  }
 }

<COLLECTION>{

  [^\n[:blank:]:\-@]* {
    BEGIN(INITIAL);
#ifdef utMAIN
    logEmit(LOG_INFO, "STRING = \"%s\"", yytext);
#else	/*	: utMAIN	*/
    yylval->string = createSizedString(yyleng, yytext);
    return(confSTRING);
#endif	/*	: utMAIN	*/
  }
 }

<*>{

  [[:blank:]]+   /* : eat up any blank character */
   #[^\r\n]*     /* : eat up comments */

  (\n|\r\n) {
    ++((ConfExtra*)yyextra)->lineNo;
 /* #ifdef utMAIN */
 /*     ++env.progBar.cur; */
 /* #endif */
  }

  . { /* : eat up any unmatched character */
    logEmit(LOG_ERR, "line %i: exit on scanner error: '%s'", 
	    ((ConfExtra*)yyextra)->lineNo, yytext);

    /* generate a parsor error (unexpected $end) */
#ifdef utMAIN
    return -1;
#else
    //yylval->string = createSizedString(yyleng, yytext); // (not used)
    return confERROR;
#endif
  }
 }

%%
   /* user code (literally copied to the generated source) :
      ==========*/

/************************************************************************/

#ifdef utMAIN
#include "../misc/command.h"
GLOBAL_STRUCT_DEF;

/*=======================================================================
 * Function   : usage
 * Description: Print the usage.
 * Synopsis   : static void usage(char* programName)
 * Input      : programName = the name of the program; usually argv[0].
 * Output     : N/A
 =======================================================================*/
static void 
usage(char* programName)
{
  parserUsage(programName);
  fprintf(stderr, " [ -i inputPath ]");

  parserOptions();
  fprintf(stderr, "  ---\n"
	  "  -i, --input\t\tinput file to parse\n");
  return;
}

/*=======================================================================
 * Function   : main 
 * Author     : Nicolas ROCHE
 * modif      : 2012/05/01
 * Description: Unit test for confFile module.
 * Synopsis   : utconfFile
 * Input      : N/A
 * Output     : N/A
 =======================================================================*/
int 
main(int argc, char** argv)
{
  char* inputPath = NULL;
  FILE* inputStream = stdin;
  ConfExtra extra;
  yyscan_t scanner;
  // ---
  int rc = 0;
  int cOption = EOF;
  char* programName = *argv;
  char* options = PARSER_SHORT_OPTIONS"i:";
  struct option longOptions[] = {
     {"input", required_argument, NULL, 'i'},
    PARSER_LONG_OPTIONS,
    {0, 0, 0, 0}
  };
       
  // import mdtx environment
  getEnv(&env);

  // parse the command line
  while((cOption = getopt_long(argc, argv, options, longOptions, NULL)) 
	!= EOF) {
    switch(cOption) {
      
    case 'i':
      if(isEmptyString(optarg)) {
	fprintf(stderr, 
		"%s: nil or empty argument for the input stream\n",
		programName);
	rc = 2;
      }
      else {
	if ((inputPath = (char*)malloc(sizeof(char) * strlen(optarg) + 1))
	    == NULL) {
	  fprintf(stderr, 
		  "%s: cannot allocate memory for the input stream name\n", 
		  programName);
	  rc = 3;
	}
	else {
	  strcpy(inputPath, optarg);
	}
      }
      break;
		
      GET_PARSER_OPTIONS; // generic options
    }
    if (rc) goto optError;
  }

  // export mdtx environment
  if (!setEnv(programName, &env)) goto optError;

  /************************************************************************/
  if (conf_lex_init(&scanner)) {
    logEmit(LOG_ERR, "%s", "error initializing scanner");
    goto error;
  }

  conf_set_debug(env.debugLexer, scanner);
  logEmit(LOG_DEBUG, "conf_set_debug = %i", conf_get_debug(scanner));
  
  // scan input file if defined
  if (inputPath != NULL) {
    if ((inputStream = fopen(inputPath, "r")) == NULL) {
      logEmit(LOG_ERR, "cannot open input stream: %s", inputPath); 
      goto error;
    }
  }
  
  // call scanner 
  extra.lineNo = 1;
  conf_set_extra (&extra, scanner);
  conf_set_in(inputStream, scanner);
  if (conf_lex(NULL, scanner)) goto error;
  conf_lex_destroy(scanner);
  fclose(inputStream);
  /************************************************************************/

  destroyString(inputPath);
  rc = TRUE;
 error:
  ENDINGS;
  rc=!rc;
 optError:
  exit(rc);
}
 
#endif // utMAIN

/* Local Variables: */
/* mode: c */
/* mode: font-lock */
/* mode: auto-fill */
/* End: */
