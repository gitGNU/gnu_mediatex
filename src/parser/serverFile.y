/*=======================================================================
 * Version: $Id: serverFile.y,v 1.3 2015/06/03 14:03:52 nroche Exp $
 * Project: MediaTeX
 * Module : serverFile parser
 *
 * serverFile parser

 MediaTex is an Electronic Records Management System
 Copyright (C) 2014 2015 Nicolas Roche
 
 This program is free software: you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation, either version 3 of the License, or
 any later version.
 
 This program is distributed in the hope that it will be useful,
 but WITHOUT ANY WARRANTY; without even the implied warranty of
 MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 GNU General Public License for more details.
 
 You should have received a copy of the GNU General Public License
 along with this program.  If not, see <http://www.gnu.org/licenses/>.
 =======================================================================*/

/* prologue: =======================================================*/
%{
#include "../mediatex.h"
#include "../misc/log.h"
#include "../misc/locks.h"
#include "../memory/serverTree.h"

// hack because Flex needs YYSTYPE from tab.h and Bison needs yyscan_t from .h
typedef void* yyscan_t;
%}

%union {
  off_t  number;
  off_t  size;
  time_t time;
  float  score;
  char   string[(MAX_SIZE_STRING+1)*2]; // keys needs more place
  char   hash[MAX_SIZE_HASH+1];
}

%{
// we need YYSTYPE defined first to include the Flex headers
#include "serverFile.h"

#define LINENO serv_get_lineno(yyscanner)
  
void serv_error(yyscan_t yyscanner, Collection* coll, Server* server,
		const char* message);
%}

%code provides {
/*=======================================================================
 * Version: this file is generated by BISON using serverFile.y
 * Project: MediaTeX
 * Module : serverFile parser
 *
 * serverFile parser

 MediaTex is an Electronic Records Management System
 Copyright (C) 2014 2015 Nicolas Roche
 
 This program is free software: you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation, either version 3 of the License, or
 any later version.
 
 This program is distributed in the hope that it will be useful,
 but WITHOUT ANY WARRANTY; without even the implied warranty of
 MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 GNU General Public License for more details.
 
 You should have received a copy of the GNU General Public License
 along with this program.  If not, see <http://www.gnu.org/licenses/>.
 =======================================================================*/
#include "../memory/serverTree.h"
int parseServerFile(Collection* coll, const char* path);
}

/* declarations: ===================================================*/
%defines "serverFile.tab.h"
%output "serverFile.tab.c"
%define api.prefix {serv_}
%define api.pure full
%param {yyscan_t yyscanner}
%parse-param {Collection* coll} {Server* server}
%define parse.error verbose
%verbose
%debug

%start file

%token            srvMASTER
%token            srvSERVER
%token            srvCOMMENT
%token            srvLABEL
%token            srvHOST
%token            srvNETWORKS
%token            srvGATEWAYS
%token            srvMDTXPORT
%token            srvSSHPORT
%token            srvCOLLKEY
%token            srvUSERKEY
%token            srvHOSTKEY
%token            srvPROVIDE
%token            srvCOMMA
%token            srvCOLON
%token            srvEQUAL
%token            srvENDBLOCK
%token            srvCACHESIZE
%token            srvCACHETTL
%token            srvQUERYTTL
%token            srvSUPPTTL
%token            srvMAXSCORE
%token            srvBADSCORE
%token            srvPOWSUPP
%token            srvPOWIMAGE
%token            srvFACTSUPP
%token            srvMINGEODUP

%token <string>   srvSTRING
%token <number>   srvNUMBER
%token <score>    srvSCORE
%token <hash>     srvHASH
%token <size>     srvSIZE
%token <time>     srvTIME

%%

/* grammar rules: ==================================================*/

file: //empty file 
{
  logParser(LOG_WARNING, "%s", "the server file was empty");
}
    | headers
{
  logParser(LOG_DEBUG, "%s", "file: headers");
}
    | headers stanzas
{
  logParser(LOG_DEBUG, "%s", "file: headers stanzas");
}
;

headers: headers header
{
  logParser(LOG_DEBUG, "%s", "headers: headers header");
}
       | header
{
  logParser(LOG_DEBUG, "%s", "headers: header");
}
;

header: srvMASTER srvHASH
{
  logParser(LOG_DEBUG, "line %-3i master server: %s", LINENO, $2);
  if (!(coll->serverTree->master = addServer(coll, $2))) YYABORT;
}
      | srvCOLLKEY srvSTRING
{
  strncpy(coll->serverTree->aesKey,
	  "01234567890abcdef", MAX_SIZE_AES);
  strncpy(coll->serverTree->aesKey, $2, MAX_SIZE_AES);
}
      | srvSUPPTTL srvNUMBER srvTIME
{
  coll->serverTree->scoreParam.suppTTL = $2*$3;
}
      | srvMAXSCORE srvSCORE
{
   coll->serverTree->scoreParam.maxScore = $2;
}
      | srvBADSCORE srvSCORE
{
  coll->serverTree->scoreParam.badScore = $2;
}
      | srvPOWSUPP srvSCORE
{
  coll->serverTree->scoreParam.powSupp = $2;
}
      | srvFACTSUPP srvSCORE
{
  coll->serverTree->scoreParam.factSupp = $2;
}
      | srvMINGEODUP srvNUMBER
{
  coll->serverTree->scoreParam.factSupp = $2;
}
;

stanzas: stanzas stanza
{
  logParser(LOG_DEBUG, "%s", "stanzas: stanzas stanza");
}
       | stanza
{
  logParser(LOG_DEBUG, "%s", "stanzas: stanza");
}
;

stanza: srvSERVER server lines srvENDBLOCK
{
  logParser(LOG_DEBUG, "%s", "srvSERVER server lines srvENDBLOCK");
  server->user = destroyString(server->user);
  if (isEmptyString(server->label)) {
    if (!(server->user = createString(env.confLabel))) YYABORT;
  }
  else {
    if (!(server->user = createString(server->label))) YYABORT;
  }
  if (!(server->user = catString(server->user, "-"))) YYABORT;
  if (!(server->user = 
	catString(server->user, coll->label))) YYABORT;
}
;

server: srvHASH
{
  logParser(LOG_DEBUG, "line %-3i new server: %s", LINENO, $1);
  if (!(server = addServer(coll, $1))) YYABORT;
}
;

lines: lines line
     | line

line: srvLABEL srvSTRING
{
  if (!(server->label = createString($2))) YYERROR;
}
    | srvCOMMENT srvSTRING
{
  if (!server) YYERROR;
  if (!(server->comment = createString($2))) YYERROR;
}
    | srvHOST srvSTRING
{
  if (!server) YYERROR;
  strncpy(server->host, $2, MAX_SIZE_HOST);
}
    | srvNETWORKS networks
    | srvGATEWAYS gateways
    | srvMDTXPORT srvNUMBER
{
  if (!server) YYERROR;
  if (server) server->mdtxPort = (int)$2;
}
    | srvSSHPORT srvNUMBER
{
  if (server) server->sshPort = (int)$2;
}
    | srvUSERKEY srvSTRING
{
  if (!server) YYERROR;
  if (!(server->userKey = createString($2))) YYERROR;
}
    | srvHOSTKEY srvSTRING
{
  if (!server) YYERROR;
  if (!(server->hostKey = createString($2))) YYERROR;
}
    | srvCACHESIZE srvNUMBER srvSIZE
{
  if (server) server->cacheSize = $2*$3;
}
    | srvCACHETTL srvNUMBER srvTIME
{
  if (server) server->cacheTTL = $2*$3;
}
    | srvQUERYTTL srvNUMBER srvTIME
{
  if (server) server->queryTTL = $2*$3;
}
    | srvPROVIDE images
;

images: images srvCOMMA image
      | image
;

image: srvHASH srvCOLON srvNUMBER srvEQUAL srvSCORE
{
  Archive* archive = 0;
  Image* image = 0;

  // score is log as 0 if we log it in 4rst position ?!
  // score is log as previous value if we log it in 3rd position ?!
  logParser(LOG_DEBUG, "line %-3i new image %s:%lli", LINENO, $1, $3);
  logParser(LOG_DEBUG, "score = %.2f", $5);

  if (server) {
    if (!(archive = addArchive(coll, $1, $3))) YYERROR;
    if (!(image = addImage(coll, server, archive))) YYERROR;
    image->score = $5;
  } else {
    logParser(LOG_DEBUG, "%s", "a mon avis ne passe jamais lÃ ");
    YYABORT;
  }
}
;

networks: networks srvCOMMA network
        | network
;

gateways: gateways srvCOMMA gateway
        | gateway
;

network: srvSTRING
{
  logParser(LOG_DEBUG, "line %-3i network: %s", LINENO, $1);
  if (!addNetworkToRing(server->networks, $1)) YYABORT;
}
;

gateway: srvSTRING
{
  logParser(LOG_DEBUG, "line %-3i gateway: %s", LINENO, $1);
  if (!addNetworkToRing(server->gateways, $1)) YYABORT;
}
;

%%

/* epilogue: =======================================================*/


/*=======================================================================
 * Function   : parsererror
 * Description: Emit an error message. Called by the parser on error
 *              state.
 * Synopsis   : void parsererror(char* message)
 * Input      : char* message = the error message.
 * Output     : N/A
 =======================================================================*/
void serv_error(yyscan_t yyscanner, Collection* coll, Server* server,
		const char* message)
{
  logEmit(LOG_ERR, "%s on token '%s' line %i\n",
	  message, serv_get_text(yyscanner), LINENO);
}


/*=======================================================================
 * Function   : parseServerList
 * Description: Parse a file
 * Synopsis   : int parseServerList(const char* path, int debugFlag)
 * Input      : const char* path: the file to parse
 * Output     : TRUE on success
=======================================================================*/
int parseServerFile(Collection* coll, const char* path)
{ 
  int rc = FALSE;
  FILE* inputStream = stdin;
  yyscan_t scanner;
  Server* server = 0;

  checkCollection(coll);
  logParser(LOG_INFO, "parse %s servers", coll->label);

  // initialise scanner
  if (serv_lex_init(&scanner)) {
    logEmit(LOG_ERR, "%s", "error initializing scanner");
    goto error;
  }

  // scan input file if defined (else stdin)
  inputStream = stdin;
  if (path != 0) {
    if (!(inputStream = fopen(path, "r"))) {
      logEmit(LOG_ERR, "cannot open input stream: %s", path); 
      goto error;
    }
    if (!lock(fileno(inputStream), F_RDLCK)) goto error;
    logEmit(LOG_DEBUG, "parse server file: %s", path);
  }
  serv_set_in(inputStream, scanner);

  // debug mode for scanner
  serv_set_debug(env.debugLexer, scanner);
  logEmit(LOG_DEBUG, "serv_set_debug = %i", 
	  serv_get_debug(scanner));

  // call the parser
  if (serv_parse(scanner, coll, server)) {
    logEmit(LOG_ERR, "servers file parser error on line %i",
	    serv_get_lineno(scanner));
    logEmit(LOG_ERR, "please edit %s", path);
    goto error;
  }

  if (inputStream != stdin) {
    if (!unLock(fileno(inputStream))) goto error;
    fclose(inputStream);
  }
  rc = TRUE;
 error:
  if (!rc) {
    logEmit(LOG_ERR, "%s", "servers file parser error");
  }
  serv_lex_destroy(scanner);
  return rc;
}

/************************************************************************/

#ifdef utMAIN
#include "../misc/command.h"
#include "confFile.tab.h"
#include "supportFile.tab.h"
GLOBAL_STRUCT_DEF;

/*=======================================================================
 * Function   : usage
 * Description: Print the usage.
 * Synopsis   : static void usage(char* programName)
 * Input      : programName = the name of the program; usually argv[0].
 * Output     : N/A
 =======================================================================*/
static void 
usage(char* programName)
{
  parserUsage(programName);
  fprintf(stderr, " [ -i inputPath ]");
  
  parserOptions();
  fprintf(stderr, "  ---\n"
	  "  -i, --input\t\tinput file to parse\n");
  return;
}


/*=======================================================================
 * Function   : main 
 * Author     : Nicolas ROCHE
 * modif      : 2012/05/01
 * Description: unitary test for ServerFile module
 * Synopsis   : ./utserverFile.tab
 * Input      : -i inputPath
 * Output     : Should display the same content
 =======================================================================*/
int 
main(int argc, char** argv)
{
  Configuration* conf = 0;
  Collection* coll = 0;
  char* inputPath = 0;
  // ---
  int rc = 0;
  int cOption = EOF;
  char* programName = *argv;
  char* options = PARSER_SHORT_OPTIONS"i:";
  struct option longOptions[] = {
     {"input", required_argument, 0, 'i'},
    PARSER_LONG_OPTIONS,
    {0, 0, 0, 0}
  };

  // import mdtx environment
  env.debugParser = TRUE;
  getEnv(&env);

  // parse the command line
  while((cOption = getopt_long(argc, argv, options, longOptions, 0)) 
	!= EOF) {
    switch(cOption) {
      
    case 'i':
      if(isEmptyString(optarg)) {
	fprintf(stderr, 
		"%s: nil or empty argument for the input stream\n",
		programName);
	rc = 2;
      }
      else {
	if ((inputPath = (char*)malloc(sizeof(char) * strlen(optarg) + 1))
	    == 0) {
	  fprintf(stderr, 
		  "%s: cannot allocate memory for the input stream name\n", 
		  programName);
	  rc = 3;
	}
	else {
	  strcpy(inputPath, optarg);
	}
      }
      break;
		
      GET_PARSER_OPTIONS; // generic options
    }
    if (rc) goto optError;
  }

  // export mdtx environment
  if (!setEnv(programName, &env)) goto optError;

  /************************************************************************/
  if (!(conf = getConfiguration())) goto error;
  if (!parseSupports(conf->supportDB)) goto error;
  if (!parseConfiguration(conf->confFile)) goto error;
  if (!(coll = getCollection("coll1"))) goto error;
  if (!expandCollection(coll)) goto error;

  if (!parseServerFile(coll, inputPath)) goto error;
  if (!serializeServerTree(coll)) goto error;
  /************************************************************************/

  rc = TRUE;
 error:
  freeConfiguration();
  destroyString(inputPath);
  ENDINGS;
  rc=!rc;
 optError:
  exit(rc);
}
 
#endif // utMAIN

/* Local Variables: */
/* mode: c */
/* mode: font-lock */
/* mode: auto-fill */
/* End: */
